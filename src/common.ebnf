/* ================================ COMMON RULES ================================ */


ActionContents    ::= ActionDraw | ActionAllDraw | ActionOpponentDraw | ActionDiscardHandAndDraw | ActionDiscardHandReserveAndDraw | ActionDiscardAnyFromReserveAndDraw | ActionAsleepAndBoost | ActionModifyStatus | ActionCostReduction | ActionCreateToken | ActionGainBoost | ActionRamp | ActionHandToReserve | ActionPickupFromReserve | ActionPickupFromReserveOtherThanMe | ActionOwnerSacrifice | ActionAllSacrifice | ActionExpeditionMovesBack | ActionRemoval | ActionPlayForFreeAsleep | ActionSabotage | ActionRollDieXBoosts | ActionRollDie4Plus | ActionActivateJTarget | ActionActivateJAll | ActionResupply | ActionAllResupply | ActionOpponentDiscard | ActionReturnToReserveIfNotFleeting | ActionSwitchExpedition | ActionAfterYou | ActionNextPlayed | ActionExpeditionMoveForward | ActionWinTheGame | ActionGainCounters

ActionWinTheGame    ::= "you win the game"

ActionCostReduction ::= "the next" CostReductionObject "you play this " ("afternoon" | "turn") " costs" ManaValue "less"
CostReductionObject ::= WS* (CardType | CharacterType | "card") WS*

ActionDraw      ::= "draw" TextQuantity "card" "s"? 
ActionAllDraw   ::= "each player draws" TextQuantity "card" "s"?
ActionOpponentDraw ::= "target opponent draws" TextQuantity "card" "s"?
ActionDiscardHandAndDraw ::= "each player discards their hand, then draws" TextQuantity "card" "s"?
ActionDiscardHandReserveAndDraw ::= "each player discards their hand and their reserve, then draws" TextQuantity "card" "s"?

ActionDiscardAnyFromReserveAndDraw ::= "you may discard any number of cards from your reserve to draw that many cards"

ActionResupply  ::= "[resupply]"
ActionAllResupply ::= "each player may [resupply]"

ActionAfterYou   ::= "[after you]"
ActionSabotage   ::= "[sabotage]"

ActionExpeditionMoveForward ::= "target expedition moves forward one region"

ActionSacrificeSelfOptional ::= "you may sacrifice me"
ActionOwnerSacrifice    ::= "sacrifice" TextQuantity "character" "s"? (WS* SacrificeRestrictionMyExpedition)?
ActionAllSacrifice      ::= "each player sacrifices" TextQuantity "character" "s"?
SacrificeRestrictionMyExpedition ::= "in my expedition"


ActionReturnToReserveIfNotFleeting ::= "if i'm not [[fleeting]]: put me in reserve"

ActionSwitchExpedition  ::= "target character switches expedition"

ActionPlayForFreeAsleep ::= "you may play me for free and i gain" WS* CharacterStatusAsleep

ActionAsleepAndBoost    ::= "up to one target character gains [[asleep]]. you may have it gain" Amount "boosts"

ActionActivateJTarget   ::= ActionOptional? WS* "activate the {j} " ("abilities" | "triggers") " of " ("up to" TextQuantity)? "target" WS* (CardTypePermanent | CardTypeCharacter) WS* "you control"
ActionActivateJAll ::= "for each" " other"? WS* (CardTypeCharacter | CardTypePermanent) WS* "you control" " other than me"? ", you may activate its {j} " ("abilities" | "triggers")

ActionModifyStatus      ::=  ActionSelfBoostAndLoseStatus | ActionSelfGainStatus | ActionSelfLoseStatus | ActionTargetGainStatus | ActionTargetLoseStatus | ActionTargetOtherLoseStatus | ActionAllControlledGainStatus | ActionAllTargetExpeditionGainStatus
ActionSelfGainStatus    ::= ("i gain" | "have me gain") CharacterStatus
ActionSelfLoseStatus    ::= "i lose" CharacterStatus
ActionTargetGainStatusOne ::= "target character" WS* TargetStatusRestriction? WS* "gains" CharacterStatus
ActionTargetGainStatus  ::= ActionTargetGainStatusOne | "up to" TextQuantity "target character" "s"? WS* TargetStatusRestriction? "gain" "s"? CharacterStatus
ActionTargetLoseStatus  ::= "up to" TextQuantity "target character" "s"? WS* TargetStatusRestriction? "lose" "s"? CharacterStatus
ActionTargetOtherLoseStatus ::= "you may have " ("target character other than me" | "another target character") " lose" CharacterStatus WS* ActionTargetOtherLoseStatusAndGainBoost?
ActionSelfBoostAndLoseStatus ::= "i gain" Amount BOOST "and lose" CharacterStatus
ActionTargetOtherLoseStatusAndGainBoost ::= "and gain" Amount BOOST
ActionAllControlledGainStatus ::= "each character controlled by target player gains" CharacterStatus
ActionAllTargetExpeditionGainStatus ::= "all characters in target expedition gain" CharacterStatus

TargetStatusRestriction    ::= (RestrictionHandCostLessThan | RestrictionHandCostMoreThan)? WS* RestrictionOtherThanMe? WS*


ActionGainBoost            ::= ActionBoostForReserve | ActionBoostForLandmark | ActionSelfGainBoost | ActionTargetGainBoost | ActionMultiTargetGainBoost | ActionAllGainBoost | ActionAllTypeGainBoost | ActionTriggerObjectGainsBoost
ActionSelfGainBoost        ::= ("i gain" | "have me gain") Amount BOOST
ActionTriggerObjectGainsBoost ::= ("it gains" | "give it") Amount BOOST
ActionTargetGainBoost      ::= ("you may have "? "target character" | "up to one target" WS* CharacterType) WS* TargetBoostRestriction? WS* "gain" "s"? Amount BOOST
ActionMultiTargetGainBoost ::= "up to" TextQuantity "target characters each gain" Amount BOOST
ActionAllGainBoost         ::= "characters you control gain" Amount BOOST
ActionAllTypeGainBoost     ::= CharacterType WS* "you control" WS* RestrictionOtherThanMe? WS* "gain" Amount BOOST
ActionBoostForReserve   ::= "i gain 1 boost" "[]"? WS* "for each card in" WS* (ActionBoostForReserveAllReserve | ActionBoostForReserveOwnReserve)
ActionBoostForReserveOwnReserve ::= "your reserve"
ActionBoostForReserveAllReserve ::= "each player's reserve"
ActionBoostForLandmark     ::= "i gain 1 boost for each landmark you control"

TargetBoostRestriction     ::= RestrictionYouControl? WS* RestrictionInOtherExpedition?

ActionNextPlayed ::= ActionNextCharacterLosesFleeting | ActionNextSpellLosesFleeting | ActionNextCharacterGainsBoost | ActionNextCharacterActivatesR | ActionNextSpellIsFree
ActionNextCharacterLosesFleeting ::= "the next character you play this turn loses" WS* CharacterStatusFleeting
ActionNextSpellLosesFleeting     ::= "the next spell you play this turn loses" WS* CharacterStatusFleeting
ActionNextCharacterGainsBoost    ::= "the next character you play this turn gains" Amount BOOST
ActionNextCharacterActivatesR    ::= ("the next character you play from your hand this turn activates its {r} abilities" | "activate the {r} triggers of the next character you play from your hand this turn")
ActionNextSpellIsFree            ::= "the next spell you play this turn is free" ActionNextSpellLosesFleetingIfFromHand?
ActionNextSpellLosesFleetingIfFromHand ::= ". if you play it from your hand, it loses" WS* CharacterStatusFleeting

ActionRollDie4Plus         ::= "roll a die. on a 4+" (","|":") WS* ActionContents WS* ActionRollDieOtherwise?
ActionRollDieOtherwise     ::= (". otherwise," | ". on a 1-3,") WS* ActionContents

ActionRollDieXBoosts        ::= "roll a die." DieBoostSubject "gain" "s"? " x boosts, where x is the result"
DieBoostSubject            ::= WS* (DieBoostSelf | DieBoostTargetCharacter) WS*
DieBoostSelf               ::= "i"
DieBoostTargetCharacter    ::= "up to one target character" | "target character"

ActionGainCounters      ::= "i gain" TextQuantity CounterType "counters"

CounterType         ::= WS* CounterTypeKelon WS*
CounterTypeKelon    ::= "kelon"

ActionCreateToken       ::= "create" TextQuantity TokenType WS* "token" "s"? ","? WS* CreateTokenLocation

TokenType               ::= TokenTypeOrdisRecruit | TokenTypeBrassbug
TokenTypeOrdisRecruit   ::= "[ordis recruit 1/1/1] soldier"
TokenTypeBrassbug       ::= "[brassbug 2/2/2] robot"

CreateTokenLocation     ::= CreateTokenLocationMyExpedition | CreateTokenLocationOtherExpedition | CreateTokenLocationEachExpedition | CreateTokenLocationTargetExpedition | CreateTokenLocationHeroExpedition | CreateTokenLocationCompanionExpedition | CreateTokenLocationDistributed
CreateTokenLocationMyExpedition     ::= "in my expedition"
CreateTokenLocationOtherExpedition  ::= "in your other expedition" " (the one i'm not in)"?
CreateTokenLocationEachExpedition   ::= "in each of your expeditions"
CreateTokenLocationTargetExpedition ::= "in target expedition"
CreateTokenLocationHeroExpedition   ::= "in your hero expedition"
CreateTokenLocationCompanionExpedition ::= "in your companion expedition"
CreateTokenLocationDistributed      ::= "distributed as you choose among any number of target expeditions"

ActionRamp              ::= ActionRampBothPlayers | ActionRampMoveMe | ActionRampFromDeckOne | ActionRampFromDeckMany
ActionRampBothPlayers   ::= "each player puts the top card of their deck in their mana zone (as an exhausted mana orb)"
ActionRampMoveMe        ::= ActionOptional? WS* "put me in my owner's mana zone (as an exhausted mana orb)" (ActionRampMoveMeOtherwiseDraw)?
ActionRampFromDeckOne   ::= "put the top card of your deck in your mana zone (as an exhausted mana orb)"
ActionRampFromDeckMany  ::= "put the top" TextQuantity "cards of your deck in your mana zone (as exhausted mana orbs)"

ActionRampMoveMeOtherwiseDraw ::= ". if you don't, draw a card"

ActionHandToReserve     ::= ActionOptional? WS* "put a card from your hand in reserve"
ActionPickupFromReserve ::= ActionOptional? WS* "return a" WS* (CardType | "card") WS* "from your reserve to your hand"
ActionPickupFromReserveOtherThanMe ::= ActionOptional? WS* "return " ("a card other than me" | "another card") " from your reserve to your hand"

ActionExpeditionMovesBack       ::= "your opponent's expedition facing " ("me" | "mine") " moves backwards one region"

ActionOpponentDiscard     ::= "target opponent discards a card from their hand"

ActionRemoval             ::= ActionRemoveDiscard | ActionReturnToHand | ActionSendSelfToReserve |ActionSendToReserveCondition | ActionSendToReserve | ActionSendToTopDeck | ActionSendToManaOrb

ActionRemoveDiscard       ::= "you may discard target" ActionRemovalStatusRestriction? CardTypeDiscardable ActionRemovalRestrictions?
ActionReturnToHand        ::= "you may return target" CardTypeDiscardable ActionRemovalRestrictions? " to its owner's hand"
ActionSendSelfToReserve   ::= "put me in reserve"
ActionSendToReserveCondition ::= "you may send to reserve target character" WS* ActionRemovalRestrictions
ActionSendToReserve       ::= "you may send target character to reserve"
ActionSendToTopDeck       ::= "you may return target" CardTypeDiscardable "to the top of its owner's deck"
ActionSendToManaOrb       ::= "you may put target" CardTypeDiscardable "in its owner's mana zone (as an exhausted mana orb)"

ActionRemovalStatusRestriction ::= CharacterStatus
ActionRemovalRestrictions ::= RestrictionHandCostLessThanCharacters | RestrictionHandCostMoreThan | RestrictionHandCostLessThan

RestrictionOtherThanMe      ::= "other than me"
RestrictionHandCostLessThan ::= "with hand cost" ManaValue "or less"
RestrictionHandCostMoreThan ::= "with hand cost" ManaValue "or more"
RestrictionHandCostLessThanCharacters ::= "with hand cost {x} or less, where x is the number of characters you control"
RestrictionInOtherExpedition ::= "in your other expedition" " (the one i'm not in)"?
RestrictionYouControl       ::= "you control"

CharacterStatus         ::= WS* (CharacterStatusChooseAny | CharacterStatusFleeting | CharacterStatusAnchored | CharacterStatusAsleep) WS*
CharacterStatusChooseAny ::= "[[fleeting]], [[anchored]] or [[asleep]]"
CharacterStatusFleeting ::= "[[fleeting]]"
CharacterStatusAnchored ::= "[[anchored]]"
CharacterStatusAsleep   ::= "[[asleep]]"

ActionOptional      ::= "you may"

AbilityWord         ::= AbilityTough1 | AbilityTough2 | AbilityGigantic | AbilitySeasoned | AbilityDefender | AbilityEternal
AbilityTough1       ::= "[tough 1]"
AbilityTough2       ::= "[tough 2]"
AbilityGigantic     ::= "[gigantic]"
AbilitySeasoned     ::= "[seasoned]"
AbilityDefender     ::= "[defender]"
AbilityEternal      ::= "[eternal]"

CardType            ::= WS* (CardTypePermanent | CardTypeCharacter | CardTypeSpell) WS*
CardTypeDiscardable ::= WS* (CardTypeCharacterOrPermanent | CardTypePermanent | CardTypeCharacter) WS*

CardTypePermanent   ::= "permanent" "s"?
CardTypeCharacter   ::= "character" "s"?
CardTypeSpell       ::= "spell" "s"?
CardTypeCharacterOrPermanent ::= "character" "s"? " or permanent" "s"?

TextQuantity    ::= WS* (TextQuantity1 | TextQuantity2 | TextQuantity3 | TextQuantity4 | TextQuantity5 | TextQuantity6 | TextQuantity7 | TextQuantity8) TextQuantityOrMore? WS*
TextQuantity1   ::= "one" | "an" | "a"
TextQuantity2   ::= "two"
TextQuantity3   ::= "three"
TextQuantity4   ::= "four"
TextQuantity5   ::= "five"
TextQuantity6   ::= "six"
TextQuantity7   ::= "seven"
TextQuantity8   ::= "eight"
TextQuantityOrMore   ::= " or more"

CharacterType       ::= CharacterTypePlant | CharacterTypeRobot | CharacterTypeBureaucrat
CharacterTypePlant  ::= "plant" "s"?
CharacterTypeRobot  ::= "robot" "s"?
CharacterTypeBureaucrat ::= "bureaucrat" "s"?

ReminderText        ::= WS* '(' (#x20 | #x5B | #x5D | [0-9a-z.,'/{}+-])* ')'

ManaValue           ::= WS* '{' DIGIT+ '}' WS*
Amount              ::= WS* (DIGIT+) WS*

BOOST               ::= WS* "boost" "s"? "[]"? WS*

DASH                ::= WS* DASH_SYMBOL WS*
COLON               ::= WS* COLON_SYMBOL WS*

DIGIT               ::= ([#x30-#x39])
WS                  ::= ([#x20]) 
DASH_SYMBOL         ::= "â€”"
COLON_SYMBOL        ::= ":"
